apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

group='com.github.vivitale'

android {
	compileSdkVersion 30
	buildToolsVersion "30.0.3"
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	defaultConfig {
		minSdkVersion 19
		targetSdkVersion 30
		versionCode 1
		versionName "0.0.1"

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
		multiDexEnabled true
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		debug {
			//            applicationIdSuffix ".debug"
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		unsigned {
			//			versionNameSuffix '-unsigned'
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}
}

dependencies {
	api fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
	// 已经整合进入BaseCore中的库
	implementation 'androidx.core:core-ktx:1.3.2'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	implementation 'androidx.appcompat:appcompat:1.3.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.legacy:legacy-support-v13:1.0.0'
	implementation 'androidx.fragment:fragment-ktx:1.3.5'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.percentlayout:percentlayout:1.0.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
	implementation 'com.google.android.material:material:1.3.0'
	// RxJava
	implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
	implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
	// json相关
	api 'com.google.code.gson:gson:2.9.0'
	// Knife
	implementation 'com.jakewharton:butterknife:10.2.1'
	annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
	// EventBus
	implementation 'org.greenrobot:eventbus:3.2.0'
	//沉浸式
	api 'com.readystatesoftware.systembartint:systembartint:1.0.4'
	// 动画
	api 'com.nineoldandroids:library:2.4.0'
	api 'com.github.florent37:viewanimator:1.1.2'
	//腾讯MMKV 取代sp
	api 'com.tencent:mmkv:1.2.7'

	// 依据项目情况需自行整合的库
	// 网络请求
	compileOnly 'com.lzy.net:okgo:3.0.4' //可以单独使用，不需要依赖下方的扩展包
	compileOnly 'com.lzy.net:okrx2:2.0.2' //RxJava扩展支持，根据需要添加
	compileOnly 'com.lzy.net:okserver:2.0.5' //版本号使用 + 可以自动引用最新版
	compileOnly 'com.squareup.okhttp3:okhttp:3.11.0'
	compileOnly 'com.squareup.okio:okio:2.2.2'
	// 图片工具glide
	compileOnly 'com.github.bumptech.glide:glide:4.12.0'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
	compileOnly 'com.github.bumptech.glide:okhttp3-integration:4.12.0'
	// RecyclerView 适配器工具
	compileOnly 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'
	// 轮播图
	implementation 'com.github.bingoogolapple:BGABanner-Android:3.0.1'
	// 二维码,一维码工具
	compileOnly 'cn.bingoogolapple:bga-qrcode-zxing:1.3.7'
	compileOnly 'cn.bingoogolapple:bga-qrcode-zbar:1.3.7'
	// 日历控件
	// compileOnly 'com.squareup:android-times-square:1.6.5@aar'
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
		}
	}
}

// ---------------------- JitPack ----------------------
// 打包源码
task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	failOnError  false
	source = android.sourceSets.main.java.sourceFiles
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
